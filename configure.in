dnl $Id: configure.in,v 1.62 2002/11/26 08:30:28 cegger Exp $
dnl Process this file with autoconf to produce a configure script.

AC_PREREQ(2.13)

AC_INIT(include/ggi/ggi.h)

AM_INIT_AUTOMAKE(libggi,2.0.2-rc2,-)

AM_MAINTAINER_MODE
AM_DISABLE_STATIC

dnl ========================================================================
dnl Set some defaults

PATHTAG="pAtHTAg"
TAGLEN="7"
ggi_conffile="libggi.conf"
ggi_subdir="ggi"

use_debug="yes"

use_threads="no"
THREADLIBS=""

sublib_libs=""
dynload_libs="-lgii -lgg"
bsdsock_libs=""

dnl Common useable Targets
build_x_target="auto"
build_xlib_target="no"
build_file_target="auto"
build_kgi_target="no"
build_libkgi_target="no"
build_suidkgi_target="no"
build_memory_target="yes"
build_dga_target="auto"
internal_xf86dga="yes"
build_terminfo_target="auto"

dnl Linux specific Targets
build_aa_target="auto"
build_fbdev_target="auto"
build_directfb_renderer="auto"
build_glide_target="auto"
build_lcd823_target="auto"
build_monotext_target="auto"
build_svga_target="auto"
build_vcsa_target="auto"

dnl FreeBSD specific Targets
build_vgl_target="auto"

dnl Win32 specific Targets
build_directx_target="no"

dnl PseudoTargets
build_multi_target="yes"
build_tile_target="auto"
build_palemu_target="auto"
build_truemu_target="auto"
build_sub_target="auto"
build_ipc_target="auto"
build_tele_target="auto"

dnl Helpers
build_mansync_helper="auto"
build_vgagl_helper="auto"
build_linvtsw_helper="auto"

dnl SWAR selection: yes = compile this swar, no = don't compile,
dnl auto = compiles the swar if CC supports it,
dnl build = compiles the swar if the CPU on the build system supports it.
dnl do_64bitc="auto"
dnl do_mmx="auto"
dnl do_3dnow="auto"
dnl do_vis="auto"
dnl do_mvi="auto"

dnl Disable SWARs for two reasons:
dnl 1. SWARs are still alpha
dnl 2. People have encountered compiling problems with asm code
do_noswar="yes"
do_64bitc="no"
do_mmx="no"
do_3dnow="no"
do_vis="no"
do_mvi="no"


dnl ========================================================================
dnl Checks for programs.

AC_PROG_CC
AC_LIBTOOL_DLOPEN
AC_LIBTOOL_WIN32_DLL
AM_PROG_LIBTOOL

dnl ========================================================================
dnl User selectable options


dnl Bah! autoconf hasn't set prefix to it's default value yet!
# if $prefix is set here then gcc warns about reordered system and non-system
# dirs. ac_err=`grep -v '^ *+' conftest.out | grep -v "^conftest.${ac_ext}\$"`
# should be ac_err=`grep -v '^ *+' conftest.out | grep -v "^conftest.${ac_ext}\$" | grep -v '^[^\:]*: warning:'` if we don't drop.
# (ac_err should ignore warnings produced by $ac_try_prog too)
# Note, that this might be a autoconf 2.13 issue.
# When we do not set prefix here, then --with-gii does NOT default to --prefix then.
test "x$prefix" = xNONE && prefix=$ac_default_prefix
test "x$exec_prefix" = xNONE && exec_prefix='${prefix}'


dnl TARGETS:

dnl Common useable Targets
AC_ARG_ENABLE(x,
[  --disable-x             don't build the X target  (uses memory-target as backbuffer)],
build_x_target=$enableval)
AC_ARG_ENABLE(xlib,
[  --disable-xlib          don't build the Xlib target],
build_xlib_target=$enableval)
AC_ARG_ENABLE(file,
[  --disable-file          don't build the file target],
build_file_target=$enableval)
AC_ARG_ENABLE(kgi,
[  --disable-kgi           don't build the kgi target],
build_kgi_target=$enableval)
AC_ARG_ENABLE(libkgi,
[  --disable-libkgi        don't build the libkgi target],
build_libkgi_target=$enableval)
dnl AC_ARG_ENABLE(suidkgi,
dnl [  --enable-suidkgi        build the suidkgi target],
dnl build_suidkgi_target=$enableval)
AC_ARG_ENABLE(memory,
[  --disable-memory        don't build the memory target],
build_memory_target=$enableval)
AC_ARG_ENABLE(dga,
[  --disable-dga           don't build the dga target],
build_dga_target=$enableval)
AC_ARG_ENABLE(internal-xf86dga,
[  --disable-internal-xf86dga  don't use libggi internal xf86dga support],
internal_xf86dga=$enableval)
AC_ARG_ENABLE(terminfo,
[  --disable-terminfo      don't build the terminfo target],
build_terminfo_target=$enableval)


dnl Linux specific Targets
AC_ARG_ENABLE(aa,
[  --disable-aa            don't build the aa target],
build_aa_target=$enableval)
AC_ARG_ENABLE(fbdev,
[  --disable-fbdev         don't build the fbdev target],
build_fbdev_target=$enableval)
AC_ARG_ENABLE(directfb,
[  --disable-directfb      don't build the directfb renderer],
build_directfb_renderer=$enableval)
AC_ARG_WITH(directfb,
[  --with-directfb=DIR     find directfb drivers modules in DIR],
  build_directfb_renderer=yes
  directfb_driver_dir=$withval,
  directfb_driver_dir=/usr/lib/directfb/gfxdrivers)
AC_ARG_ENABLE(glide,
[  --disable-glide         don't build the glide target],
build_glide_target=$enableval)
AC_ARG_ENABLE(monotext,
[  --disable-monotext      don't build the monotext target],
build_monotext_target=$enableval)
AC_ARG_ENABLE(svga,
[  --disable-svga          don't build the svga target],
build_svga_target=$enableval)
AC_ARG_ENABLE(vcsa,
[  --disable-vcsa          don't build the vcsa target],
build_vcsa_target=$enableval)


dnl FreeBSD specific Targets
AC_ARG_ENABLE(vgl,
[  --disable-vgl           don't build the vgl target],
build_vgl_target=$enableval)


dnl Win32 specific Targets
AC_ARG_ENABLE(directx,
[  --disable-directx       don't build the directx target],
build_directx_target=$enableval)


dnl PseudoTargets
AC_ARG_ENABLE(multi,
[  --disable-multi         don't build the multi target],
build_multi_target=$enableval)
AC_ARG_ENABLE(tile,
[  --disable-tile          don't build the tile target],
build_tile_target=$enableval)
AC_ARG_ENABLE(palemu,
[  --disable-palemu        don't build the palemu target],
build_palemu_target=$enableval)
AC_ARG_ENABLE(trueemu,
[  --disable-trueemu       don't build the trueemu target],
build_trueemu_target=$enableval)
AC_ARG_ENABLE(sub,
[  --disable-sub           don't build the sub target],
build_sub_target=$enableval)
AC_ARG_ENABLE(ipc,
[  --disable-ipc            don't build the ipc target],
build_ipc_target=$enableval)
AC_ARG_ENABLE(tele,
[  --disable-tele          don't build the tele target],
build_tele_target=$enableval)


dnl Helpers
AC_ARG_ENABLE(vgagl,
[  --disable-vgagl         don't build the vgagl target, needed by svga],
build_vgagl_helper=$enableval)
AC_ARG_ENABLE(linvtsw,
[  --disable-linvtsw       don't build the linvtsw helper],
build_linvtsw_helper=$enableval)



AC_ARG_WITH(gii,
[  --with-gii=DIR          use the LibGII installed DIR],
CFLAGS="$CFLAGS -I$withval/include"
  CPPFLAGS="$CPPFLAGS -I$withval/include"
  LDFLAGS="$LDFLAGS -L$withval/lib",
CFLAGS="$CFLAGS -I$prefix/include"
  CPPFLAGS="$CPPFLAGS -I$prefix/include"
  LDFLAGS="$LDFLAGS -L$prefix/lib")

# This is for building against an uninstalled libgii for
# both $(top_builddir) == $(top_srcdir)
# and $(top_builddir) != $(top_srcdir)
# I'd like the sed expression to be more robust, but
# character set matching '[]' is stripped by m4
AC_ARG_WITH(uninst-gii,
[  --with-uninst-gii=DIR   use uninstalled copy of LibGII found in DIR],
[[if test -d "$withval" ; then
     gii_top_builddir="`cd "$withval" ; pwd`"
     gii_top_srcdir="$gii_top_builddir/`cd "$withval" ; sed -n -e 's/^top_srcdir[ 	]*=[ 	]*//p' Makefile`"
     gii_top_srcdir="`cd $gii_top_srcdir ; pwd`"
   fi
   if test -d "$gii_top_builddir" -a -d "$gii_top_srcdir" ; then
     if test "$gii_top_builddir" = "$gii_top_srcdir" ; then
       CFLAGS="$CFLAGS -I$gii_top_builddir/include"
       CPPFLAGS="$CPPFLAGS -I$gii_top_builddir/include"
     else
       CFLAGS="$CFLAGS -I$gii_top_srcdir/include -I$gii_top_builddir/include"
       CPPFLAGS="$CPPFLAGS -I$gii_top_srcdir/include -I$gii_top_builddir/include"
     fi
     LDFLAGS="$LDFLAGS -L$gii_top_builddir/gg -L$gii_top_builddir/gii"
   fi
]])

AC_ARG_ENABLE(threads,
[  --enable-threads        use threads for mansync],
use_threads=$enableval)

AC_ARG_ENABLE(debug,
[  --disable-debug         don't build with run-time debugging (speed freaks)],
use_debug=$enableval)

AC_ARG_ENABLE(64bitc,
[  --disable-64bitc        don't build 64-bit C SWAR code],
do_64bitc=$enableval)

AC_ARG_ENABLE(noswar,
[  --disable-noswar        don't build non-SWAR code],
do_noswar=$enableval)

AC_ARG_ENABLE(mmx,
[  --disable-mmx           don't build MMX SWAR code],
do_mmx=$enableval)

AC_ARG_ENABLE(vis,
[  --disable-vis           don't build VIS SWAR code],
do_vis=$enableval)

AC_ARG_ENABLE(mvi,
[  --disable-mvi           don't build MVI SWAR code],
do_mvi=$enableval)

GGI_EXTRA_PATHS


dnl ========================================================================
dnl Checks for header files.

AC_HEADER_DIRENT
AC_HEADER_STDC
AC_HEADER_SYS_WAIT
AC_CHECK_HEADERS(ggi/gii.h limits.h unistd.h fcntl.h \
sys/select.h pthread.h sys/ioctl.h signal.h sys/time.h sys/shm.h sys/ipc.h \
sys/wait.h asm/io.h sys/io.h sys/kd.h linux/kd.h sys/vt.h linux/vt.h \
linux/kdev_t.h linux/major.h glide.h glide/glide.h kgimon.h sys/un.h \
sys/socket.h netinet/in.h netdb.h windows.h ddraw.h vgl.h)

dnl ========================================================================
dnl LibGII must be built

gii_missing_part=no

dnl Testing libs should use libtool but doesn't - grrrr!
dnl This isn't a problem during the build because libtool is always used.
dnl Wish I could use $LIBTOOL, but it is written for Makefile use.
save_CC="$CC"
CC="$SHELL ./libtool $CC"
save_LDFLAGS="$LDFLAGS"
LDFLAGS="$LDFLAGS -lgg"
AC_CHECK_LIB(gii,giiInit,
  AC_CHECK_LIB(gii,giiEventsQueued,
	foo=bar,
	AC_MSG_ERROR(You have an obsolete LibGII version installed!)
  ),
  gii_missing_part=yes
)
CC="$save_CC"
LDFLAGS="$save_LDFLAGS"

if test "$ac_cv_header_ggi_gii_h" != "yes"; then
  gii_missing_part=yes
fi

dnl ========================================================================
dnl Checks for typedefs, structures, and compiler characteristics.

AC_C_CONST
AC_C_INLINE
AC_TYPE_SIZE_T

GGI_DLLEXT

dnl check for socklen_t
dnl Define HAVE_SOCKETLEN_T if found.
if test "$ac_cv_header_sys_socket_h" = "yes"; then

	AC_MSG_CHECKING([whether socklen_t is available])
	AC_TRY_COMPILE([   
		#include <sys/types.h>
		#include <sys/socket.h>
		#ifdef HAVE_SYS_UN_H
		#include <sys/un.h>
		#endif], [socklen_t len;],
		[AC_MSG_RESULT(yes)
		AC_DEFINE(HAVE_SOCKLEN_T)],
		[AC_MSG_RESULT(no)])
fi

dnl ========================================================================
dnl Checks for library functions.

AC_FUNC_ALLOCA
AC_FUNC_MEMCMP
AC_FUNC_MMAP
AC_FUNC_VPRINTF
AC_CHECK_FUNCS(gettimeofday select strdup strerror setenv getopt random \
	herror connect gethostbyname sigpending fork \
	snprintf strncat strncpy)


dnl ========================================================================
dnl Checks whether to compile in various SWARs

if test "x$do_noswar" = "xyes"; then
   AC_DEFINE(DO_SWAR_NONE)
fi

AC_MSG_CHECKING(if LibGII build detected working int64)
AC_TRY_CPP([
#include <ggi/system.h>
#ifdef GG_HAVE_INT64
#else
#error No 64-bit ints - this line makes compilation fail...
#endif
],

dnl do test for do_64bitc == "build" here.
  if test "x$do_64bitc" = "xauto"; then
    do_64bitc="yes"
  fi
  AC_MSG_RESULT(yes),

  AC_MSG_RESULT(no)
  if test "x$do_64bitc" = "xyes"; then
    AC_MSG_ERROR(Cannot build 64bit C SWAR against 32-bit LibGG)
  fi
  if test "x$do_mvi" = "xyes"; then
    AC_MSG_ERROR(Cannot build MVI SWAR against 32-bit LibGG)
  fi
  if test "x$do_vis" = "xyes"; then
    AC_MSG_ERROR(Cannot build VIS SWAR against 32-bit LibGG)
  fi
  do_64bitc="no"
  do_vis="no"
  do_mvi="no")

if test "x$do_64bitc" = "xyes"; then 
   AC_DEFINE(DO_SWAR_64BITC)
fi

AC_CC_CAN_EMMS
if test "x$do_mmx" = "xyes"; then 
   if test "x$ac_cc_can_emms" != "xyes"; then
      AC_MSG_ERROR([MMX requested, but compiler cannot compile MMX!])
      do_mmx=no
   fi
fi
if test "x$do_mmx" = "xbuild"; then
   AC_CPU_CAN_EMMS
   if test "x$ac_cpu_can_emms" = "xyes"; then
      do_mmx=auto
   else
      do_mmx=no
   fi
fi
if test "x$do_mmx" = "xauto"; then
   if test "x$ac_cc_can_emms" = "xyes"; then
      do_mmx=yes
   else
      do_mmx=no
   fi
fi
if test "x$do_mmx" = "xyes"; then 
   AC_DEFINE(DO_SWAR_MMX)
fi

AC_CC_CAN_FEMMS
if test "x$do_3dnow" = "xyes"; then
   if test "x$ac_cc_can_femms" != "xyes"; then
      AC_MSG_ERROR([3DNOW requested, but compiler cannot compile 3DNOW!])
      do_3dnow=no
   fi
fi
if test "x$do_3dnow" = "xbuild"; then
   AC_CPU_CAN_FEMMS
   if test "x$ac_cpu_can_femms" = "xyes"; then
      do_3dnow=auto
   else
      do_3dnow=no
   fi
fi
if test "x$do_3dnow" = "xauto"; then
   if test "x$ac_cc_can_femms" = "xyes"; then
      do_3dnow=yes
   else
      do_3dnow=no
   fi
fi
if test "x$do_3dnow" = "xyes"; then 
   AC_DEFINE(DO_SWAR_3DNOW)
fi

AC_CC_CAN_FALIGNDATA
if test "x$do_vis" = "xyes"; then
   if test "x$ac_cc_can_faligndata" != "xyes"; then
      AC_MSG_ERROR([VIS requested, but compiler cannot compile VIS!])
      do_vis=no
   fi
fi
if test "x$do_vis" = "xbuild"; then
   AC_CPU_CAN_FALIGNDATA
   if test "x$ac_cpu_can_faligndata" = "xyes"; then
      do_vis=auto
   else
      do_vis=no
   fi
fi
if test "x$do_vis" = "xauto"; then
   if test "x$ac_cc_can_faligndata" = "xyes"; then
      do_vis=yes
   else
      do_vis=no
   fi
fi
if test "x$do_vis" = "xyes"; then 
   AC_DEFINE(DO_SWAR_VIS)
fi

AC_CC_CAN_PKLB
if test "x$do_mvi" = "xyes"; then
   if test "x$ac_cc_can_pklb" != "xyes"; then
      AC_MSG_ERROR([MVI requested, but compiler cannot compile MVI!])
      do_mvi=no
   fi
fi
if test "x$do_mvi" = "xbuild"; then
   AC_CPU_CAN_PKLB
   if test "x$ac_cpu_can_pklb" = "xyes"; then
      do_mvi=auto
   else
      do_mvi=no
   fi
fi
if test "x$do_mvi" = "xauto"; then
   if test "x$ac_cc_can_pklb" = "xyes"; then
      do_mvi=yes
   else
      do_mvi=no
   fi
fi
if test "x$do_mvi" = "xyes"; then 
   AC_DEFINE(DO_SWAR_MVI)
fi

dnl ========================================================================
dnl Checks for target-specific libraries and headers


if test "x$ac_cv_func_connect" = "xno"; then
  SAVELIBS="$LIBS"
  AC_MSG_CHECKING([for connect in -lwsock32])
  LIBS="$LIBS -lwsock32"
  AC_TRY_LINK([
#include <winsock.h>
],[
connect(0, NULL, 42);
],
    bsdsock_libs="$bsdsock_libs -lwsock32"
    ac_cv_func_connect=yes
    AC_MSG_RESULT(yes),
      AC_MSG_RESULT(no)
      LIBS="$SAVELIBS"
      AC_CHECK_LIB(socket,connect,bsdsock_libs="$bsdsock_libs -lsocket")
  )
  LIBS="$SAVELIBS"
fi

if test "x$ac_cv_func_gethostbyname" = "xno"; then
  AC_CHECK_LIB(c, gethostbyname, ,
    AC_CHECK_LIB(nsl, gethostbyname, bsdsock_libs="$bsdsock_libs -lnsl")
  )
fi


AC_CHECK_LIB(kgimon,set_monitor,
  monitest_extrasrc="kgitune.c"
  monitest_extraobj="kgitune.o"
  monitest_extralibs="-lkgimon",
  monitest_extrasrc=""
  monitest_extraobj=""
  monitest_extralibs="")

SHMDEMOS=""

AC_MSG_CHECKING(if we should build the cube3d program)
if test "x$ac_cv_header_unistd_h" = "xyes" \
	-a "x$ac_cv_header_signal_h" = "xyes" \
	-a "x$ac_cv_func_setenv" = "xyes" \
	-a "x$ac_cv_header_sys_shm_h" = "xyes" \
	-a "x$ac_cv_header_sys_ipc_h" = "xyes"; then
  SHMDEMOS="$SHMDEMOS cube3d"
  AC_MSG_RESULT(yes)
else
  AC_MSG_RESULT(no)
fi

DISPLAYSUBDIRS=""
DEFAULTSUBDIRS="stubs color text_16 text_32 linear_1 linear_1_r linear_2 linear_4 linear_4_r linear_8 linear_16 linear_24 linear_32 planar ilbm iplanar_2p"
XSUBDIRS=""
FBDEVSUBDIRS=""

have_pthreads=no
if test "x$use_threads" != "xno"; then
  AC_MSG_CHECKING(for pthread library)
  TMP_SAVE_LIBS=$LIBS
  LIBS="$LIBS -lpthread"
  AC_TRY_LINK([
#include "confdefs.h"
#define __C_ASM_H /* fix for retarded Digital Unix headers */
#include <pthread.h>],[
void foo(void)
{
pthread_exit (0);
}
],
    have_pthreads=yes
    AC_MSG_RESULT(yes),
    AC_MSG_RESULT(no))
  LIBS=$TMP_SAVE_LIBS
fi

AC_MSG_CHECKING(if we should use pthreads for mansync)
if test "$have_pthreads" = "yes" -a "x$use_threads" != "xno"; then 
  AC_TRY_CPP([
#include <features.h>
if !defined(__linux__)
#error We're not on a bad system - this line makes compilation fail...
#endif
],
    use_threads="no"
    AC_MSG_RESULT(no),
    AC_MSG_RESULT(yes))
else
      use_threads="no"
      AC_MSG_RESULT(no)
fi

memory_dependent="x"
if test "x$build_memory_target" = "xno"; then
  AC_MSG_WARN(the following targets depend on the memory target)
  AC_MSG_WARN(and will not be built: $memory_dependent)
  build_x_target="no"
fi

mansync_dependent="x aa tile palemu trueemu"
AC_MSG_CHECKING(if we should build mansync helper)
if test "x$ac_cv_func_fork" = "xno" -a "x$use_threads" = "xno"; then
  build_mansync_helper=no
fi
if test "x$build_mansync_helper" = "xno"; then
  AC_MSG_RESULT(no)
  AC_MSG_WARN(the following targets depend on the mansync helper)
  AC_MSG_WARN(and will not be built: $mansync_dependent)
  build_x_target="no"
  build_aa_target="no"
  build_tile_target="no"
  build_palemu_target="no"
  build_trueemu_target="no"
else
  DISPLAYSUBDIRS="$DISPLAYSUBDIRS mansync"
  build_mansync_helper="yes"
  AC_MSG_RESULT(yes)
fi

if test "x$build_x_target" != "xno" \
	-o "x$build_xlib_target" != "xno" \
	-o "x$build_dga_target" != "xno"; then
  AC_PATH_XTRA
  if test "x$no_x" = "xyes"; then
    build_x_target="no"
    build_xlib_target="no"
    build_dga_target="no"
  else

dnl This is necessary as there are plattforms (i.e. Darwin),
dnl where $ac_x_includes doesn't belong to the default paths

    cflags_old="$CFLAGS"
    cppflags_old="$CPPFLAGS"
    CFLAGS="$CFLAGS -I$ac_x_includes"
    CPPFLAGS="$CPPFLAGS -I$ac_x_includes"

    AC_CHECK_HEADERS(X11/extensions/xf86dga.h	\
		X11/extensions/Xdbe.h		\
		X11/extensions/XEVI.h		\
		X11/extensions/XShm.h		\
		X11/extensions/xf86vmode.h)
    CFLAGS="$cflags_old"
    CPPFLAGS="$cppflags_old"
  fi
fi

if test "x$build_aa_target" != "xno"; then
  AC_CHECK_HEADER(aalib.h,
	AC_CHECK_LIB(aa, aa_autoinit, foo=bar, build_aa_target="no"),
	build_aa_target="no")
fi

if test "x$ac_cv_func_mmap_fixed_mapped" != "xyes"; then 
  build_fbdev_target="no"
  build_file_target="no"
  build_kgi_target="no"
fi

if test "x$build_fbdev_target" != "xno"; then
  AC_CHECK_HEADER(linux/fb.h,
    AC_TRY_COMPILE([
#include <linux/fb.h>
],[
struct fb_fix_screeninfo *fix;
fix->accel = 0;
],
      AC_DEFINE(HAVE_NEW_FBDEV)
      have_new_fbdev=yes),
    build_fbdev_target="no")
fi

if test "x$build_directfb_renderer" != "xno"; then
  AC_CHECK_HEADER(directfb-internal/directfb_version.h,
	foo=bar, build_directfb_renderer="no")
fi

if test "x$build_libkgi_driver" != "xno"; then
  AC_CHECK_LIB(galloc, ggiGAInit, foo=bar, build_libkgi_target="no")
fi

if test "x$build_kgi_target" != "xno"; then
  AC_CHECK_HEADER(kgi/kgi.h,
    AC_TRY_COMPILE([
#include "kgi/config.h"
#include <kgi/system.h>
#include <kgi/kgi.h>
],[
kgi_u32_t i;
], foo=bar), build_kgi_target="no")
fi

if test "x$build_glide_target" != "xno"; then
  if test "x$ac_cv_header_glide_h" = "xno" -a \
	  "x$ac_cv_header_glide_glide_h" = "xno"; then
    build_glide_target="no"
  else 
    AC_CHECK_LIB(glide2x, grGlideInit, foo=bar,
	build_glide_target="no", [ -lm ])
  fi
fi

if test "x$build_ipc_target" != "xno"; then
  AC_CHECK_HEADERS(sys/types.h sys/socket.h sys/un.h sys/shm.h,
	build_ipc_target="yes", build_ipc_target="no")
fi

case "$host" in
  powerpc-*-linux*)
	;;
  *)
	build_lcd823_target="no"
	;;
esac


if test "x$build_vgl_target" != "xno"; then
    if test "x$ac_cv_header_vgl.h" = "xno"; then
	build_vgl_target="no"
    else
	AC_CHECK_LIB(vgl, VGLInit, foo=bar,
		     build_vgl_target="no")
    fi
fi

if test "x$build_svga_target" != "xno"; then
  AC_CHECK_HEADER(vga.h,
	AC_CHECK_LIB(vga, vga_init, foo=bar,
		build_svga_target="no"; build_vgagl_helper="no"),
	build_svga_target="no"; build_vgagl_helper="no")
  AC_CHECK_HEADER(vgagl.h,
	AC_CHECK_LIB(vgagl, gl_setpixel, foo=bar,
		build_vgagl_helper="no",
		[ -lvga ]),
	build_vgagl_helper="no")
fi

if test "x$build_terminfo_target" != "xno"; then
  curseslib=""
  AC_CHECK_HEADER(ncurses.h,
	[AC_DEFINE(HAVE_NCURSES_H)
	curseslib="ncurses"],[
  AC_CHECK_HEADER(ncurses/ncurses.h,
	[AC_DEFINE(HAVE_NCURSES_NCURSES_H)
	curseslib="ncurses"],[
  AC_CHECK_HEADER(curses.h,
	[AC_DEFINE(HAVE_CURSES_H)
	curseslib="curses"],[
	build_terminfo_target="no"])])])
  if test -n "$curseslib"; then
    AC_CHECK_LIB($curseslib, set_term,
	terminfolibs="-l$curseslib",
	build_terminfo_target=no)
  fi
fi

if test "x$build_vcsa_target" != "xno"; then
  if test "$ac_cv_header_linux_vt_h" != yes; then
    build_vcsa_target="no"
  fi
fi

AM_CONDITIONAL(INTERNAL_XF86DGA, test "x$internal_xf86dga" = "xyes")
if test "x$build_dga_target" != "xno"; then
  AC_CHECK_HEADER(X11/extensions/xf86dga.h,[
  if test "x$internal_xf86dga" = "xyes"; then
    xf86dga_libs=""
    AC_DEFINE(GGI_INTERNAL_XF86DGA)
  else
    AC_CHECK_LIB(Xxf86dga, XF86DGAQueryVersion, foo=bar,
	build_dga_target="no",
	[ $X_LIBS -lXext -lX11 $X_EXTRA_LIBS ])
    AC_CHECK_LIB(Xxf86vm, XF86VidModeSwitchToMode, foo=bar,
	build_dga_target="no",
	[ $X_LIBS -lXext -lX11 $X_EXTRA_LIBS ])
    xf86dga_libs="-lXxf86dga -lXxf86vm"
  fi
  ], build_dga_target="no")
fi

if test "x$build_linvtsw_helper" != "xno"; then
  if test "$ac_cv_header_linux_vt_h" != yes -o \
	"$ac_cv_header_linux_kd_h" != yes ; then
    build_linvtsw_helper="no"
  fi
fi

dnl check for Unix domain sockets.
dnl Define HAVE_UNIX_DOMAIN_SOCKET if found.
AC_MSG_CHECKING(whether UNIX domain sockets are supported)
AC_CACHE_VAL(ggi_cv_have_unix_domain_socket,
[AC_TRY_COMPILE([
#include <sys/types.h>
#include <sys/socket.h>
#ifdef HAVE_SYS_UN_H
#include <sys/un.h>
#endif], [struct sockaddr_un unix_addr;],
    ggi_cv_have_unix_domain_socket=yes, ggi_cv_have_unix_domain_socket=no)])
AC_MSG_RESULT($ggi_cv_have_unix_domain_socket)
if test $ggi_cv_have_unix_domain_socket = yes ; then
  AC_DEFINE(HAVE_UNIX_DOMAIN_SOCKET)
fi

dnl ========================================================================
dnl Check for targets

AC_MSG_CHECKING(if we should build x        target)
if test "x$build_x_target" = "xno"; then
  AC_MSG_RESULT(no)
else
  DISPLAYSUBDIRS="$DISPLAYSUBDIRS X"
  build_x_target="yes"
  AC_MSG_RESULT(yes)

  AC_MSG_CHECKING(if we should build support for DoubleBuffer extension for x target)
  if test "x$ac_cv_header_X11_extensions_Xdbe_h" != "xyes"; then
	AC_MSG_RESULT(no)
  else
	XSUBDIRS="$XSUBDIRS dbe"
	AC_MSG_RESULT(yes)
  fi

  AC_MSG_CHECKING(if we should build support for Extended Visual Information extension for x target)
  if test "x$ac_cv_header_X11_extensions_XEVI_h" != "xyes"; then
	AC_MSG_RESULT(no)
  else
	XSUBDIRS="$XSUBDIRS evi"
	AC_MSG_RESULT(yes)
  fi

  AC_MSG_CHECKING(if we should build support for MIT-SHM extension for x target)
  if test "x$ac_cv_header_X11_extensions_XShm_h" != "xyes"; then
	AC_MSG_RESULT(no)
  else
	XSUBDIRS="$XSUBDIRS shm"
	AC_MSG_RESULT(yes)
  fi

  AC_MSG_CHECKING(if we should build support for xf86dga extension for x target)
  if test "x$ac_cv_header_X11_extensions_xf86dga_h" != "xyes"; then
	AC_MSG_RESULT(no)
  else
	XSUBDIRS="$XSUBDIRS dga"
	AC_MSG_RESULT(yes)
  fi

  AC_MSG_CHECKING(if we should build support for XF86 Vidmode extension for x target)
  if test "x$ac_cv_header_X11_extensions_xf86vmode_h" != "xyes"; then
	AC_MSG_RESULT(no)
  else
	XSUBDIRS="$XSUBDIRS vidmode"
	AC_MSG_RESULT(yes)
  fi
fi

AC_MSG_CHECKING(if we should build xlib     target)
if test "x$build_xlib_target" = "xno"; then
  AC_MSG_RESULT(no)
else
  DISPLAYSUBDIRS="$DISPLAYSUBDIRS Xlib"
  build_xlib_target="yes"
  AC_MSG_RESULT(yes)
fi

AC_MSG_CHECKING(if we should build libkgi   target)
if test "x$build_libkgi_target" = "xno"; then
   AC_MSG_RESULT(no)
 else
   DISPLAYSUBDIRS="$DISPLAYSUBDIRS libkgi"
   build_libkgi_target="yes"
   AC_MSG_RESULT(yes)
fi

AC_MSG_CHECKING(if we should build kgi      target)
if test "x$build_kgi_target" = "xno"; then
  AC_MSG_RESULT(no)
else
  DISPLAYSUBDIRS="$DISPLAYSUBDIRS kgi"
  DEFAULTSUBDIRS="$DEFAULTSUBDIRS kgi"
  build_kgi_target="yes"
  AC_MSG_RESULT(yes)
fi

AC_MSG_CHECKING(if we should build aa       target)
if test "x$build_aa_target" = "xno"; then
  AC_MSG_RESULT(no)
else
  DISPLAYSUBDIRS="$DISPLAYSUBDIRS aa"
  build_aa_target="yes"
  AC_MSG_RESULT(yes)
fi

AC_MSG_CHECKING(if we should build directx  target)
if test "x$build_directx_target" != "xno" -a \
	"x$ac_cv_header_ddraw_h" = "xyes"; then
  DISPLAYSUBDIRS="$DISPLAYSUBDIRS directx"
  build_directx_target="yes"
  AC_MSG_RESULT(yes)
else
  AC_MSG_RESULT(no)
fi

AC_MSG_CHECKING(if we should build fbdev    target)
if test "x$build_fbdev_target" = "xno"; then
  AC_MSG_RESULT(no)
  if test "x$build_directfb_renderer" != "xno"; then
    AC_MSG_WARN(directfb renderer requires fbdev!)
    AC_MSG_WARN(     disabling directfb renderer)
    AC_MSG_WARN(     enable fbdev or disable directfb to disable warning)
    build_directfb_renderer="no"
  fi
else
  DISPLAYSUBDIRS="$DISPLAYSUBDIRS fbdev"
  if test "x$have_new_fbdev" = "xyes"; then
    DEFAULTSUBDIRS="$DEFAULTSUBDIRS fbdev"
  fi
  build_fbdev_target="yes"
  AC_MSG_RESULT(yes)

  AC_MSG_CHECKING(if we should build directfb renderer )
  if test "x$build_directfb_renderer" = "xno"; then
    AC_MSG_RESULT(no)
  else
    build_directfb_renderer="yes"
    FBDEVSUBDIRS="$FBDEVSUBDIRS directfb"
    AC_DEFINE_UNQUOTED(DIRECTFB_DRIVER_DIR,"$directfb_driver_dir")
    AC_MSG_RESULT(yes)
  fi
fi

AC_MSG_CHECKING(if we should build file     target)
if test "x$build_file_target" = "xno"; then
  AC_MSG_RESULT(no)
else
  DISPLAYSUBDIRS="$DISPLAYSUBDIRS file"
  build_file_target="yes"
  AC_MSG_RESULT(yes)
fi

AC_MSG_CHECKING(if we should build glide    target)
if test "x$build_glide_target" = "xno"; then
  AC_MSG_RESULT(no)
else
  DISPLAYSUBDIRS="$DISPLAYSUBDIRS glide"
  build_glide_target="yes"
  AC_MSG_RESULT(yes)
fi

AC_MSG_CHECKING(if we should build ipc      target)
if test "x$build_ipc_target" = "xno"; then
  AC_MSG_RESULT(no)
else
  DISPLAYSUBDIRS="$DISPLAYSUBDIRS ipc"
  build_ipc_target="yes"
  AC_MSG_RESULT(yes)
fi

dnl AC_MSG_CHECKING(if we should build suidkgi  target)
dnl if test "x$build_suidkgi_target" = "xno"; then
dnl   AC_MSG_RESULT(no)
dnl else
dnl   DISPLAYSUBDIRS="$DISPLAYSUBDIRS suidkgi"
dnl   DEFAULTSUBDIRS="$DEFAULTSUBDIRS linmm_banked ramdac ioctl"
dnl   build_suidkgi_target="yes"
dnl   AC_MSG_RESULT(yes)
dnl fi

AC_MSG_CHECKING(if we should build lcd823   target)
if test "x$build_lcd823_target" = "xno"; then
  AC_MSG_RESULT(no)
else
  DISPLAYSUBDIRS="$DISPLAYSUBDIRS lcd823"
  build_lcd823_target="yes"
  AC_MSG_RESULT(yes)
fi

AC_MSG_CHECKING(if we should build memory   target)
if test "x$build_memory_target" = "xno"; then
  AC_MSG_RESULT(no)
else
  DISPLAYSUBDIRS="$DISPLAYSUBDIRS memory"
  build_memory_target="yes"
  AC_MSG_RESULT(yes)
fi

AC_MSG_CHECKING(if we should build monotext target)
if test "x$build_monotext_target" = "xno"; then
  AC_MSG_RESULT(no)
else
  DISPLAYSUBDIRS="$DISPLAYSUBDIRS monotext"
  build_monotext_target="yes"
  AC_MSG_RESULT(yes)
fi

AC_MSG_CHECKING(if we should build multi    target)
if test "x$build_multi_target" = "xno"; then
  AC_MSG_RESULT(no)
else
  DISPLAYSUBDIRS="$DISPLAYSUBDIRS multi"
  build_multi_target="yes"
  AC_MSG_RESULT(yes)
fi

AC_MSG_CHECKING(if we should build palemu   target)
if test "x$build_palemu_target" = "xno"; then
  AC_MSG_RESULT(no)
else
  DISPLAYSUBDIRS="$DISPLAYSUBDIRS palemu"
  build_palemu_target="yes"
  AC_MSG_RESULT(yes)
fi

AC_MSG_CHECKING(if we should build sub      target)
if test "x$build_sub_target" = "xno"; then
  AC_MSG_RESULT(no)
else
  DISPLAYSUBDIRS="$DISPLAYSUBDIRS sub"
  build_sub_target="yes"
  AC_MSG_RESULT(yes)
fi

AC_MSG_CHECKING(if we should build vgl      target)
if test "x$build_vgl_target" = "xno"; then
  AC_MSG_RESULT(no)
else
  DISPLAYSUBDIRS="$DISPLAYSUBDIRS vgl"
  build_vgl_target="yes"
  AC_MSG_RESULT(yes)
fi

AC_MSG_CHECKING(if we should build svga     target)
if test "x$build_svga_target" = "xno"; then
  AC_MSG_RESULT(no)
else
  DISPLAYSUBDIRS="$DISPLAYSUBDIRS svgalib"
  build_svga_target="yes"
  AC_MSG_RESULT(yes)
fi

AC_MSG_CHECKING(if we should build vgagl    helper (used by svga target))
if test "x$build_vgagl_helper" = "xno"; then
  AC_MSG_RESULT(no)
else
  DISPLAYSUBDIRS="$DISPLAYSUBDIRS vgagl"
  build_vgagl_helper="yes"
  AC_MSG_RESULT(yes)
fi

AC_MSG_CHECKING(if we should build tele     target)
if test "x$build_tele_target" = "xno"; then
  AC_MSG_RESULT(no)
else
  DISPLAYSUBDIRS="$DISPLAYSUBDIRS tele"
  build_tele_target="yes"
  AC_MSG_RESULT(yes)
fi

AC_MSG_CHECKING(if we should build terminfo target)
if test "x$build_terminfo_target" = "xno"; then
  AC_MSG_RESULT(no)
else
  DISPLAYSUBDIRS="$DISPLAYSUBDIRS terminfo"
  build_terminfo_target="yes"
  AC_MSG_RESULT(yes)
fi

AC_MSG_CHECKING(if we should build tile     target)
if test "x$build_tile_target" = "xno"; then
  AC_MSG_RESULT(no)
else
  DISPLAYSUBDIRS="$DISPLAYSUBDIRS tile"
  build_tile_target="yes"
  AC_MSG_RESULT(yes)
fi

AC_MSG_CHECKING(if we should build trueemu  target)
if test "x$build_trueemu_target" = "xno"; then
  AC_MSG_RESULT(no)
else
  DISPLAYSUBDIRS="$DISPLAYSUBDIRS trueemu"
  build_trueemu_target="yes"
  AC_MSG_RESULT(yes)
fi

AC_MSG_CHECKING(if we should build vcsa     target)
if test "x$build_vcsa_target" = "xno"; then
  AC_MSG_RESULT(no)
else
  DISPLAYSUBDIRS="$DISPLAYSUBDIRS vcsa"
  build_vcsa_target="yes"
  AC_MSG_RESULT(yes)
fi

AC_MSG_CHECKING(if we should build dga      target)
if test "x$build_dga_target" = "xno"; then
  AC_MSG_RESULT(no)
else
  DISPLAYSUBDIRS="$DISPLAYSUBDIRS xf86dga"
  build_dga_target="yes"
  AC_MSG_RESULT(yes)
fi

AC_MSG_CHECKING(if we should build linvtsw  helper)
if test "x$build_linvtsw_helper" = "xno"; then
  AC_MSG_RESULT(no)
else
  DISPLAYSUBDIRS="$DISPLAYSUBDIRS linvtsw"
  build_linvtsw_helper="yes"
  AC_MSG_RESULT(yes)
fi


dnl ========================================================================
dnl Finally add some flags

dnl _THREAD_SAFE is used instead of _REENTRANT on some systems
CFLAGS="$CFLAGS -D_REENTRANT -D_THREAD_SAFE"

if test "x$use_debug" = "xyes"; then
  CFLAGS="$CFLAGS -DDEBUG"
  if test "$ac_cv_prog_gcc" = "yes"; then
    CFLAGS="$CFLAGS -g -Wall"
  fi
fi


dnl ========================================================================
dnl Write output

AC_SUBST(THREADLIBS)
AC_SUBST(sublib_libs)
AC_SUBST(dynload_libs)
AC_SUBST(bsdsock_libs)

AC_SUBST(DISPLAYSUBDIRS)
AC_SUBST(DEFAULTSUBDIRS)
AC_SUBST(XSUBDIRS)
AC_SUBST(FBDEVSUBDIRS)

dnl Target specific substitutions
AC_SUBST(terminfolibs)
AC_SUBST(xf86dga_libs)
AC_SUBST(directfb_driver_dir)

dnl Program specific substitutions
AC_SUBST(SHMDEMOS)
AC_SUBST(monitest_extrasrc)
AC_SUBST(monitest_extraobj)
AC_SUBST(monitest_extralibs)

dnl Use static_* to substitute into files where values shouldn't
dnl dynamicly change.  Makefiles need to be able to dynamicly change
dnl paths between build and install and shouldn't use these static_*.
dnl Files that will be installed must always show the final location where
dnl they will reside and should use these static_* values.
dnl Ensure that all static_* are fully expanded.

eval static_libdir="$libdir"
old_val=""
until test "$static_libdir" = "$old_val"; do
	old_val="$static_libdir"
	eval static_libdir="$static_libdir"
done

eval static_sysconfdir="$sysconfdir"
old_val=""
until test "$static_sysconfdir" = "$old_val"; do
	old_val="$static_sysconfdir"
	eval static_sysconfdir="$static_sysconfdir"
done

AC_SUBST(ggi_subdir)
AC_SUBST(static_libdir)
AC_SUBST(static_sysconfdir)
AC_DEFINE_UNQUOTED(GGITAGLEN,$TAGLEN)
AC_DEFINE_UNQUOTED(GGIPATHTAG,"$PATHTAG")
AC_DEFINE_UNQUOTED(GGICONFFILE,"$ggi_conffile")
AC_DEFINE_UNQUOTED(GGICONFDIR,"$PATHTAG$static_sysconfdir/$ggi_subdir")

if test "x$use_threads" = "xpthread"; then
  AC_DEFINE(USE_THREADS)
  THREADLIBS="-lpthread"
fi

AM_CONFIG_HEADER(config.h)

AC_OUTPUT(
Makefile
ggi/Makefile
include/Makefile
include/ggi/Makefile
include/ggi/internal/Makefile
include/ggi/internal/font/Makefile
include/ggi/default/Makefile
include/ggi/display/Makefile
display/Makefile
display/X/Makefile
display/X/helper/Makefile
display/X/helper/dga/Makefile
display/X/helper/vidmode/Makefile
display/X/helper/evi/Makefile
display/X/helper/dbe/Makefile
display/X/helper/shm/Makefile
display/Xlib/Makefile
display/aa/Makefile
display/common/Makefile
display/directx/Makefile
display/fbdev/Makefile
display/file/Makefile
display/glide/Makefile
display/ipc/Makefile
display/kgi/Makefile
display/lcd823/Makefile
display/libkgi/Makefile
display/linvtsw/Makefile
display/mansync/Makefile
display/memory/Makefile
display/monotext/Makefile
display/multi/Makefile
display/palemu/Makefile
display/sub/Makefile
display/svgalib/Makefile
display/vgl/Makefile
display/tele/Makefile
display/terminfo/Makefile
display/tile/Makefile
display/trueemu/Makefile
display/vcsa/Makefile
display/vgagl/Makefile
display/xf86dga/Makefile
default/Makefile
default/color/Makefile
default/common/Makefile
default/kgi/Makefile
default/kgi/ATI/Makefile
default/kgi/ATI/Mach64/Makefile
default/kgi/ATI/Radeon/Makefile
default/linear_1/Makefile
default/linear_1_r/Makefile
default/linear_2/Makefile
default/linear_4/Makefile
default/linear_4_r/Makefile
default/linear_8/Makefile
default/linear_16/Makefile
default/linear_24/Makefile
default/linear_32/Makefile
default/stubs/Makefile
default/text_16/Makefile
default/text_32/Makefile
default/ilbm/Makefile
default/planar/Makefile
default/iplanar_2p/Makefile
default/fbdev/Makefile
default/fbdev/directfb/Makefile
default/fbdev/directfb/core/Makefile
default/fbdev/directfb/core/fusion/Makefile
default/fbdev/mga/Makefile
default/fbdev/mga/2164w/Makefile
default/fbdev/mga/g400/Makefile
default/fbdev/ati/Makefile
default/fbdev/ati/mach64/Makefile
extensions/Makefile
extensions/test/Makefile
programs/Makefile
programs/demos/Makefile
programs/demos/warp-ggi/Makefile
programs/util/Makefile
programs/util/monitest/Makefile
programs/check/Makefile
doc/Makefile
doc/man/Makefile
m4/Makefile
libggi.conf
display/fbdev/fbdev.conf
display/kgi/kgi.conf
display/libkgi/libkgi.conf
dist/Makefile
dist/rpm/Makefile
dist/rpm/libggi.spec
)

dnl Removed dirs
dnl  display/suidkgi/Makefile
dnl  default/ramdac/Makefile
dnl  default/linmm_banked/Makefile
dnl  default/ioctl/Makefile

if test "$gii_missing_part" = "yes"; then 
  AC_MSG_WARN([
LibGII is not properly installed on the system. You need LibGII for
building LibGGI. Please compile LibGII first.
])
fi
